{
  parserClass="com.appian.intellij.k3.parser.KParser"
  extends="com.appian.intellij.k3.KAstWrapperPsiElement"

  psiClassPrefix="K"
  psiImplClassSuffix="Impl"
  psiPackage="com.appian.intellij.k3.psi"
  psiImplPackage="com.appian.intellij.k3.psi.impl"

  elementTypeHolderClass="com.appian.intellij.k3.psi.KTypes"
  elementTypeClass="com.appian.intellij.k3.psi.KElementType"
  tokenTypeClass="com.appian.intellij.k3.psi.KTokenType"

  psiImplUtilClass="com.appian.intellij.k3.psi.impl.KPsiImplUtil"

  tokens=[

    // ascii chars
    bang='!'
    quote='"'
    hash='#'
    dollar='$'
    percent='%'
    ampersand='&'
    open_paren='('
    close_paren=')'
    asterisk='*'
    plus='+'
    comma=','
    dash='-'
    period='.'
    colon=':'
    semicolon=';'
    less_than='<'
    equals='='
    greater_than='>'
    question_mark='?'
    at='@'
    open_bracket='['
    close_bracket=']'
    caret='^'
    underscore='_'
    back_tick='`'
    open_brace='{'
    pipe='|'
    close_brace='}'
    tilde='~'

    primitive_verb="primitive verb"

    // adverbs
    adverb="adverb"

    // names
    user_identifier="variable"
    k3_system_function="k3 function"

    // nouns
    number="number"
    char="char"
    string="string"
    symbol_vector="symbol vector"
    number_vector="number vector"

    // commands
    current_namespace="namespace"
    command="command"

    // special
    trace='\'
    signal="signal"
    mode="mode" // either k) or q)

    // control
    control="control statement"
    conditional="conditional evaluation"

    // punctuation
    newline="new line"
    comment='comment'
  ]
}

// E:E;e|e e:nve|te| t:n|v v:tA|V n:t[E]|(E)|{E}|N
root ::= top* separator_or_eof
private top ::= namespace_declaration
              | command_statement
              | mode_directive
              | expression_list
private expression_list ::= separator
                          | expression (separator+ expression)*
expression ::= noun_or_verb noun_or_verb? adverb? expression?

private noun_or_verb ::= noun
                       | primitive_verb [colon]
                       | system_function
private noun ::= control_flow
            | args // there can be standalone args: e.g. (`.fn;[])
            | group_or_list
            | primitive_noun
            | assignment
            | id

private control_flow ::= control_statement | conditional_evaluation
control_statement ::= control args
conditional_evaluation ::= conditional args
args ::= open_bracket expression_list* close_bracket

private system_function ::= k3_system_function | signal | trace | colon // ":" is return
group_or_list ::= open_paren expression_list* close_paren

lambda ::= open_brace [lambda_params] adverb? expression_list* close_brace // including adverb? here is a hack to make {[error]'error} work.
lambda_params ::= open_bracket [user_id (separator user_id)*] close_bracket

private separator ::= semicolon | newline
private separator_or_eof ::= separator | <<eof>>

// always defined at root
command_statement ::= command [expression]
namespace_declaration ::= current_namespace user_id
mode_directive ::= mode expression

// simple & compound assignment; local & global
assignment ::= user_id [args] (colon [colon] | verb colon | colon verb colon) expression {
 methods=[isCompound]
}

// all data are syntactically nouns
private primitive_noun ::= vector | atom | lambda
private vector ::= symbol_vector | number_vector | string
private atom ::= symbol | number | char

private id ::= user_id | k3_system_function
user_id ::= user_identifier {
  mixin="com.appian.intellij.k3.psi.impl.KNamedElementImpl"
  implements="com.appian.intellij.k3.psi.KNamedElement"
  methods=[getName setName getNameIdentifier getPresentation isDeclaration]
}
